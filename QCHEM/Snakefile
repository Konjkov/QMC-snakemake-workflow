import re

include: '../Snakefile'

STD_ERR = 0.0001
VMC_NCONFIG = 100000
MOLECULES = ['H']
METHODS = ['HF']
BASES = ['cc-pVQZ']
JASTROWS = ['8_8_44']
STABILITY_ANALYSIS = False

def hf_energy(*path):
    """Get QCHEM output energy."""
    regexp = re.compile(' Total energy in the final basis set =\s+(?P<energy>[-+]?\d+\.\d+)')
    with open(os.path.join(*path, 'mol.out'), 'r') as qchem_out:
        # we are only interested in the last occurrence
        energy = float(re.findall(regexp, qchem_out.read())[-1])
    return energy

def hf_time(*path):
    """Get QCHEM time."""
    regexp = re.compile(' Total job time:\s+(?P<time>\d+\.\d+)s\(wall\)')
    with open(os.path.join(*path, 'mol.out'), 'r') as qchem_out:
        # we are only interested in the last occurrence
        time = float(re.findall(regexp, qchem_out.read())[-1])
    return time

def get_up_down(*path):
    """Get up and down electron numbers from QCHEM output file.
     There are        2 alpha and        2 beta electrons
    """

    regexp = re.compile('There are\s+(?P<alpha>\d+) alpha and\s+(?P<beta>\d+) beta electrons')
    with open(os.path.join(*path, 'mol.out'), 'r') as qchem_out:
        for line in qchem_out:
            m = re.search(regexp, line)
            if m:
                neu = int(m.group('alpha'))
                ned = int(m.group('beta'))
    return neu, ned

wildcard_constraints:
    multideterminant_method='OD|OD\(2\)|VOD|VOD\(2\)|QCCD|QCCD\(2\)|VQCCD',
    order='\d+'

rule ALL:
    input:

rule ALL_VMC_DMC:
    input:
        expand('{method}/{basis}/{molecule}/VMC_DMC/emin/{jastrow}/tmax_2_1024_2/out', molecule=MOLECULES, basis=BASES, method=METHODS, jastrow=JASTROWS),

rule ALL_VMC_DMC_BF:
    input:
        expand('{method}/{basis}/{molecule}/VMC_DMC_BF/emin_BF/{jastrow}__9_9_33/tmax_2_1024_2/out', molecule=MOLECULES, basis=BASES, method=METHODS, jastrow=JASTROWS),

rule ALL_VMC_OPT_ENERGY:
    input:
        expand('{method}/{basis}/{molecule}/VMC_OPT_ENERGY/emin/{jastrow}/1000000/out', molecule=MOLECULES, basis=BASES, method=METHODS, jastrow=JASTROWS),

rule ALL_VMC_OPT_ENERGY_BF:
    input:
        expand('{method}/{basis}/{molecule}/VMC_OPT_ENERGY_BF/emin_BF/{jastrow}__9_9_33/1000000/out', molecule=MOLECULES, basis=BASES, method=METHODS, jastrow=JASTROWS),

rule ALL_VMC_OPT:
    input:
        expand('{method}/{basis}/{molecule}/VMC_OPT/emin/{jastrow}/out', molecule=MOLECULES, basis=BASES, method=METHODS, jastrow=JASTROWS),

rule ALL_VMC_OPT_BF:
    input:
        expand('{method}/{basis}/{molecule}/VMC_OPT_BF/emin_BF/{jastrow}__9_9_33/out', molecule=MOLECULES, basis=BASES, method=METHODS, jastrow=JASTROWS),

rule ALL_VMC:
    input:
        expand('{method}/{basis}/{molecule}/VMC/10000000/out', molecule=MOLECULES, basis=BASES, method=METHODS),

rule ALL_QCHEM:
    input:
        expand('{method}/{basis}/{molecule}/mol.out', molecule=MOLECULES, basis=BASES, method=METHODS),

###################################################################################################################

rule MDET:
    input:      '{method}/{basis}/{molecule}/mol.out'
    output:     '{method}/{basis}/{molecule}/correlation.data'
    run:
        if wildcards.method.split('_')[0] in ('OD', 'OD(2)', 'VOD', 'VOD(2)', 'QCCD', 'QCCD(2)', 'VQCCD'):
            value = wildcards.method.split('_')[1]
            if float(value) < 1.0:
                shell('cd "$(dirname "{output}")" && multideterminant.py 7 mol.out --amplitude "{value}"')
            else:
                shell('cd "$(dirname "{output}")" && multideterminant.py 7 mol.out --excitation "{value}"')
        else:
            shell('cd "$(dirname "{output}")" && multideterminant.py 7 mol.out')

rule GWFN:
    input:      '{method}/{basis}/{molecule}/mol.molden'
    output:     '{method}/{basis}/{molecule}/gwfn.data'
    shell:      'molden2qmc.py 7 "{input}" "{output}"'

pat1 = '======= MOLDEN-FORMATTED INPUT FILE FOLLOWS ======='
pat2 = '======= END OF MOLDEN-FORMATTED INPUT FILE ======='

rule MOLDEN:
    input:      '{method}/{basis}/{molecule}/mol.out'
    output:     '{method}/{basis}/{molecule}/mol.molden'
    params:      pattern='/%(pat1)s/,/%(pat2)s/{/%(pat1)s/!{/%(pat2)s/!p}}' % {'pat1': pat1, 'pat2': pat2}
    shell:      'sed -n "{params.pattern}" "{input}" > "{output}"'

rule QCHEM_RUN:
    input:      INPUTS_DIR + '/{molecule}.xyz'
    output:     '{method}/{basis}/{molecule}/mol.out'
    run:
        stability_analysis = STABILITY_ANALYSIS
        qchem_input = os.path.join(wildcards.method, wildcards.basis, wildcards.molecule, 'mol.in')
        for algorithm in ('diis', 'gdm'):
            # set template
            if wildcards.method.split('_')[0] in ('OD', 'OD(2)', 'VOD', 'VOD(2)', 'QCCD', 'QCCD(2)', 'VQCCD'):
                method = wildcards.method.split('_')[0]
                template = 'qchem_multideterminant.tmpl'
            else:
                method = wildcards.method
                if stability_analysis == 'true':
                    template = 'qchem.tmpl'
                else:
                    template = 'qchem_unstable.tmpl'
            with open(input[0], 'r') as f:
                f.readline()                   # skip first line
                molecule_data = f.read()[:-1]  # skip last NL
            if pp_basis(wildcards.basis):
                basis = wildcards.basis.split('_')[0]
                template = 'qchem_ecp.tmpl'

                flag = False
                external_ecp = ''
                with open('ppotential/DiracFock_AREP_gaussian.bas') as ecp:
                    for line in ecp:
                        for symbol in get_atomic_symbols(wildcards.molecule):
                            if symbol.lower() == line.strip().lower():
                                flag = True
                        if flag:
                            external_ecp += line
                        if line.startswith('****'):
                            flag = False

                flag = False
                external_basis = ''
                with open('basis/{}.bas'.format(basis)) as basis:
                    for line in basis:
                        for symbol in get_atomic_symbols(wildcards.molecule):
                            if symbol.lower() == line.strip().lower():
                                flag = True
                        if flag:
                            external_basis += line
                        if line.startswith('****'):
                            flag = False

                with open(qchem_input, 'w') as f:
                    f.write(open(template).read().format(
                        basis=basis,
                        method=method,
                        molecule_data=molecule_data,
                        algorithm=algorithm,
                        stability_analysis=stability_analysis,
                        external_ecp=external_ecp,
                        external_basis=external_basis
                    ))
            else:
                with open(qchem_input, 'w') as f:
                    f.write(open(template).read().format(
                        basis=wildcards.basis,
                        method=method,
                        molecule_data=molecule_data,
                        algorithm=algorithm,
                        stability_analysis=stability_analysis
                    ))
            shell('qchem -nt 4 "{qchem_input}" "{output}"')
            if 'Convergence criterion met' in open(output[0]).read():
                break
